#pragma mode( separator(.,;) integer(h32) )
EXPORT Demo_Tetrahedron()
begin

  local points= {{0, 0, 0, 0}, {0, 0, 10, 0}, {0, 10, 0, 0}, {10, 5, 5, 0}, {0, 0, 0, 0}};   // Points List for our tetrahedron. Define >4 points so  definition does not appear to define a single point. 
  local triangles= {{1, 2, 3, #8080FF}, {1, 2, 4, #FFFF80}, {1, 3, 4, #80FF80}, {2, 3, 4, #FF8080}};   // Define triangles: a list of 3 points from the point list and a color 
  local lines={0,{1,2},{1,3},{1,4},{2,1},{2,3},{2,4},{3,1},{3,2},{3,4},{4,1},{4,2},{4,3}}; // Define black lines between all the points
  local rotmat= [[1, 0, 0, 0], [0, 1, 0, 0], [0, 0, 1, 0]];   // Initial rotation matrix (no rotation)
  STARTAPP("Function"); // Start function app. The next variables set are graphing app variables and do not exist for apps with no graphing capabilty.
  STARTVIEW(-1); // set home screen because STARTAPP opens the default view for the app
  Xmin:=-15.9;  Xmax:=15.9;  Ymin:=-10.9;  Ymax:=10.9; // set default Cartesian window values

  local saveangle = HAngle; //save the current HAngle setting
  HAngle:= 1; // deg mode
  dimgrob(G1,(Xmax-Xmin),(Ymax-Ymin));   // Create a graphic object of the same dimension as the screen 

  while 1 do   // forever (or until the user stops by pressing ON)   
   
    rect(G1);  // erase the grob
    local zclip= triangle_p(G1);    // create a blank zclipping string
    triangle(G1, points, triangles, {rotmat, "N"}, zclip);    // draw our transformed triangles
    line(G1, points, lines, {rotmat, "N"}, zclip);    // draw our transformed lines

    // depending on key pressed, rotate 5 degree in the appropriate direction
    if iskeydown(2) then // up
      rotmat:= rotmat*[[cos(5), sin(5), 0, 0], [-sin(5), cos(5), 0, 0], [0, 0, 1, 0], [0, 0, 0, 1]];
    end;
    if iskeydown(12) then // down
      rotmat:= rotmat*[[cos(-5), sin(-5), 0, 0], [-sin(-5), cos(-5), 0, 0], [0, 0, 1, 0], [0, 0, 0, 1]];
    end;
    if iskeydown(7) then // left
      rotmat:= rotmat*[[cos(5), 0, sin(5), 0], [0, 1, 0, 0], [-sin(5), 0, cos(5), 0], [0, 0, 0, 1]];
    end;
    if iskeydown(8) then // right
      rotmat:= rotmat*[[cos(-5), 0, sin(-5), 0], [0, 1, 0, 0], [-sin(-5), 0, cos(-5), 0], [0, 0, 0, 1]];
    end;

    blit_p(G1); // copy the background graphic on the screen
    wait(0.01); // small pause

  end;
  HAngle=saveangle; //restore the HAngle setting 
end;
